Needs["MUnit`"]

(* Test the loader function generated by `generate_loader!`. *)
TestMatch[
	load = LibraryFunctionLoad[
		"libwstp_example",
		"load_wstp_functions",
		LinkObject,
		LinkObject
	];

	$functions = load["libwstp_example"];

	Sort[$functions]
	,
	<|
		"count_args" -> LibraryFunction[_, "count_args", LinkObject],
		"expr_string_join" -> LibraryFunction[_, "expr_string_join", LinkObject],
		"link_expr_identity" -> LibraryFunction[_, "link_expr_identity", LinkObject],
		"square_wstp" -> LibraryFunction[_, "square_wstp", LinkObject],
		"string_join" -> LibraryFunction[_, "string_join", LinkObject],
		"total" -> LibraryFunction[_, "total", LinkObject],
		"total_args_i64" -> LibraryFunction[_, "total_args_i64", LinkObject]
	|>
]

Test[
	$functions["square_wstp"][4]
	,
	16
]

(* Test that passing more than one argument to square_wstp() results in a Failure. *)
TestMatch[
	$functions["square_wstp"][4, 4]
	,
	Failure["RustPanic", <|
		"MessageTemplate" -> "Rust LibraryLink function panic: `message`",
		"MessageParameters" -> <|"message" -> "square_wstp: expected to get a single argument"|>,
		"SourceLocation" -> s_?StringQ /; StringStartsQ[s, "wolfram-library-link/examples/wstp.rs:"],
		"Backtrace" -> Missing["NotEnabled"]
	|>]
]

Test[
	$functions["count_args"][a, b, c]
	,
	3
]

Test[
	totalArgsI64 = $functions["total_args_i64"];

	{
		totalArgsI64[2, 2],
		totalArgsI64[1, 2, 3]
	}
	,
	{
		4,
		6
	}
]

Test[
	stringJoin = $functions["string_join"];

	{
		stringJoin["Hello, ", "World!"],
		stringJoin[Sequence @@ CharacterRange["A", "G"]],
		stringJoin[]
	},
	{
		"Hello, World!",
		"ABCDEFG",
		""
	}
]

TestMatch[
	linkExprIdentity = $functions["link_expr_identity"];

	(* Note:
		Set $Context and $ContextPath to force symbols sent across the LinkObject to
		contain the symbol context explicitly.
	*)
	Block[{$Context = "UnusedContext`", $ContextPath = {}},
		linkExprIdentity[foo[], bar[baz]]
	]
	,
	{foo[], bar[baz]}
]

TestMatch[
	exprStringJoin = $functions["expr_string_join"];
	(* Note:
		Set $Context and $ContextPath to force symbols sent across the LinkObject to
		contain the symbol context explicitly.
	*)
	Block[{$Context = "UnusedContext`", $ContextPath = {}},
		{
			exprStringJoin[],
			exprStringJoin["Foo"],
			exprStringJoin["Foo", "Bar"],
			exprStringJoin[Sequence @@ CharacterRange["a", "f"]],
			exprStringJoin[1, 2, 3]
		}
	]
	,
	{
		"",
		"Foo",
		"FooBar",
		"abcdef",
		Failure["RustPanic", <|
			"MessageTemplate" -> "Rust LibraryLink function panic: `message`",
			"MessageParameters" -> <|"message" -> "expected String argument, got: 1"|>,
			(* Avoid hard-coding the panic line/column number into the test. *)
			"SourceLocation" -> s_?StringQ /; StringStartsQ[s, "wolfram-library-link/examples/wstp.rs:"],
			"Backtrace" -> Missing["NotEnabled"]
		|>]
	}
]

TestMatch[
	total = $functions["total"];
	(* Note:
		Set $Context and $ContextPath to force symbols sent across the LinkObject to
		contain the symbol context explicitly.
	*)
	Block[{$Context = "UnusedContext`", $ContextPath = {}},
		{
			total[],
			total[1, 2, 3],
			total[1, 2.5, 7],
			(* Cause an integer overflow. *)
			total[2^62, 2^62, 2^62],
			total[5, "Hello"]
		}
	]
	,
	{
		0,
		6,
		10.5,
		Failure["RustPanic", <|
			"MessageTemplate" -> "Rust LibraryLink function panic: `message`",
			"MessageParameters" -> <|"message" -> "attempt to add with overflow"|>,
			"SourceLocation" -> s0_?StringQ /; StringStartsQ[s0, "wolfram-library-link/examples/wstp.rs:"],
			"Backtrace" -> Missing["NotEnabled"]
		|>],
		Failure["RustPanic", <|
			"MessageTemplate" -> "Rust LibraryLink function panic: `message`",
			"MessageParameters" -> <|
				"message" -> "expected argument as position 1 to be a number, got \"Hello\""
			|>,
			"SourceLocation" -> s1_?StringQ /; StringStartsQ[s1, "wolfram-library-link/examples/wstp.rs:"],
			"Backtrace" -> Missing["NotEnabled"]
		|>]
	}
]